apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'com.neenbedankt.android-apt'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {}
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            v2SigningEnabled false
        }
    }

    buildTypes {
        buildTypes {
            debug {
                buildConfigField "boolean", "LOG_DEBUG", "true"
                minifyEnabled true
                zipAlignEnabled true
                shrinkResources true
                versionNameSuffix "-debug"
                signingConfig signingConfigs.debug
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }

            release {
                buildConfigField "boolean", "LOG_DEBUG", "true"
                //混淆
                minifyEnabled true
                //Zipalign优化
                zipAlignEnabled true

                // 移除无用的resource文件
                shrinkResources true
                //前一部分代表系统默认的android程序的混淆文件，该文件已经包含了基本的混淆声明
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                //签名
                signingConfig signingConfigs.release

                applicationVariants.all { variant ->
                    variant.outputs.each { output ->
                        def outputFile = output.outputFile
                        def author = "pzw"
                        // 输出apk名称为XX XX_v1.0_pzw_2016-01-15_release.apk
                        def fileName = "cf_hd0206a_v${defaultConfig.versionName}_${author}_${releaseTime()}_release.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:2.0.1'
    }
    /**
     schemaVersion 当前数据库结构的版本。结构版本变化时在OpenHelpers中被使用到。当你改变实体或者数据的结构时，
     这个值应该增加。
     daoPackage 生成的DAO，DaoMaster和DaoSession的包名。默认是实体的包名。
     targetGenDir 生成源文件的路径。默认源文件目录是在build目录中的(build/generated/source/greendao)。
     generateTests 设置是否自动生成单元测试。
     targetGenDirTest 生成的单元测试的根目录。
     */
    greendao {
        schemaVersion 1
        targetGenDir 'src/main/java'
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }

    lintOptions {
        abortOnError false
    }
}

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile rootProject.ext.dependencies.junit
    compile project(':common')
    compile files('libs/AraBMApiDev.jar')
    compile files('libs/AraBMApiIdcard.jar')
    compile rootProject.ext.dependencies.appcompat_v7
    compile rootProject.ext.dependencies.greendao
    compile rootProject.ext.dependencies.dagger
    apt rootProject.ext.apt.dagger_compiler
    apt rootProject.ext.apt.butterknife_compiler
    // 文件操作
    compile(rootProject.ext.dependencies.opencsv) {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    //    provided 'org.glassfish:javax.annotation:10.0-b28'
}